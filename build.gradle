apply plugin: 'java'

version = '3.0.0-C5'
group = 'org.hjson'
description = """Hjson, the Human JSON."""

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets {
  main {
    java {
      srcDir 'src/main'
    }
  }
  test {	
    java {	
      srcDir 'src/test'	
    }	
    resources {	
      srcDir 'assets'	
    }	
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
  manifest {
    attributes(
      'Hjson-Version': version,
    )
  }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task(testSuite, dependsOn: ['classes','testClasses'], type: JavaExec) {
  main = 'org.hjson.test.Main'
  classpath = files(sourceSets.main.runtimeClasspath, sourceSets.test.runtimeClasspath)
}

test.dependsOn testSuite

apply from: 'gradle-mvn-push.gradle'
